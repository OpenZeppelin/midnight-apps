// SPDX-License-Identifier: MIT
pragma language_version >= 0.14.0;

// TODO: that is mainly designed for the DEX needed but it can be extended with more generic Uint types.
/**
 * @description A utility module providing mathematical operations for Uint<128> types.
 */
module Math {
    import CompactStandardLibrary;

    export struct DivResult {
        quotient: Uint<128>,
        remainder: Uint<128>
    }

    /** @description Maximum value for Uint<8> (2^8 - 1). */
    export ledger MAX_U8: Uint<8>;

    /** @description Maximum value for Uint<16> (2^16 - 1). */
    export ledger MAX_U16: Uint<16>;

    /** @description Maximum value for Uint<32> (2^32 - 1). */
    export ledger MAX_U32: Uint<32>;

    /** @description Maximum value for Uint<64> (2^64 - 1). */
    export ledger MAX_U64: Uint<64>;

    /** @description Maximum value for Uint<128> (2^128 - 1). */
    export ledger MAX_U128: Uint<128>;

    /** @description Maximum value for Uint<256> (2^256 - 1). */
    export ledger MAX_U256: Uint<256>;

    /**
     * @description Computes the square root of a Uint<128> value locally (off-chain).
     *
     * @param radicand The number to compute the square root of.
     * @returns Uint<128> The square root of radicand.
     */
    export witness sqrtLocally(radicand: Uint<128>): Uint<128>;

    /**
     * @description Computes division of two Uint<128> values locally (off-chain).
     *
     * @param dividend The number to divide.
     * @param divisor The number to divide by.
     * @returns DivResult The quotient and remainder of the division.
     */
    export witness divLocally(dividend: Uint<128>, divisor: Uint<128>): DivResult;

    /**
     * @description Initializes the max value constants for common Uint types.
     *
     * @returns None
     */
    export circuit initialize(): [] {
        MAX_U8   = 255; // 2^8 - 1
        MAX_U16  = 65535; // 2^16 - 1
        MAX_U32  = 4294967295; // 2^32 - 1
        MAX_U64  = 18446744073709551615; // 2^64 - 1
        MAX_U128 = 340282366920938463463374607431768211455; // 2^128 - 1
        MAX_U256 = 115792089237316195423570985008687907853269984665640564039457584007913129639935; // 2^256 - 1
    }

    /**
     * @description Adds two Uint<128> numbers, checking for overflow.
     *
     * @param addend The first number to add.
     * @param augend The second number to add.
     * @returns Uint<128> The sum of addend and augend.
     */
    export circuit add(
        addend: Uint<128>,
        augend: Uint<128>
    ): Uint<128> {
        const result = addend + augend;
        assert (result <= MAX_U128) "Math: addition overflow";
        return result as Uint<128>;
    }

    /**
     * @description Subtracts subtrahend from minuend, checking for underflow.
     *
     * @param minuend The number to subtract from.
     * @param subtrahend The number to subtract.
     * @returns Uint<128> The difference between minuend and subtrahend.
     */
    export circuit sub(
        minuend: Uint<128>,
        subtrahend: Uint<128>
    ): Uint<128> {
        assert (minuend >= subtrahend) "Math: subtraction underflow";
        return minuend - subtrahend;
    }

    /**
     * @description Multiplies two Uint<128> values, returning a Uint<256> result.
     *
     * @param multiplicand The first number to multiply.
     * @param multiplier The second number to multiply.
     * @returns Uint<256> The product of multiplicand and multiplier.
     */
    export circuit mul(
        multiplicand: Uint<128>,
        multiplier: Uint<128>
    ): Uint<256> {
        return multiplicand * multiplier;
    }

    /**
     * @description Divides a Uint<128> dividend by a divisor, returning quotient.
     *
     * @param dividend The number to divide.
     * @param divisor The number to divide by.
     * @returns quotient The quotient of the division.
     * @dev Uses a local division witness and verifies the result on-chain.
     */
    export circuit div(
        dividend: Uint<128>, 
        divisor: Uint<128>
    ): Uint<128> {
        return _div(dividend, divisor).quotient;
    }

    /**
     * @description Computes the remainder of dividing dividend by divisor.
     *
     * @param dividend The number to divide.
     * @param divisor The number to divide by.
     * @returns Uint<128> The remainder of the division.
     */
    export circuit rem(
        dividend: Uint<128>,
        divisor: Uint<128>
    ): Uint<128> {
        return _div(dividend, divisor).remainder;
    }

    /**
     * @description Divides a Uint<128> dividend by a divisor, returning quotient and remainder.
     *
     * @param dividend The number to divide.
     * @param divisor The number to divide by.
     * @returns DivResult The quotient and remainder of the division.
     *
     * @dev Uses a local division witness and verifies the result on-chain.
     */
    circuit _div(
        dividend: Uint<128>, 
        divisor: Uint<128>
    ): DivResult {
        assert (divisor > 0) "Math: division by zero";
        const result = divLocally(dividend, divisor);
        assert (result.remainder < divisor) "Math: remainder error";
        assert (
            (mul(result.quotient, divisor) + result.remainder) as Uint<128> == dividend
            ) "Math: division invalid";
        return result;
    }

    /**
     * @description Computes the square root of a Uint<128> value, verified on-chain.
     *
     * The implementation uses the Newton-Raphson method via the sqrtLocally witness,
     * avoiding floating-point precision issues inherent in Math.sqrt by performing
     * all calculations with bigint arithmetic. For imperfect squares, the result is
     * rounded toward zero (i.e., floor(sqrt(radicand))).
     *
     * For perfect squares (e.g., 16), the result is exact (√16 = 4).
     * For imperfect squares (e.g., 20), the result is floored (√20 ≈ 4.47 → 4).
     * 
     * The ZK circuit enforces:
     *  - root² ≤ radicand
     *  - (root + 1)² > radicand
     *
     * @param radicand The number to compute the square root of.
     * @returns Uint<128> The square root of radicand.
     */
    export circuit sqrt(
        radicand: Uint<128>
    ): Uint<128> {
        const root = sqrtLocally(radicand);
        assert (mul(root, root) as Field as Uint<222> <= radicand as Field as Uint<222>) "Math: sqrt overestimate (root² > radicand)";
        assert (mul(add(root, 1) , add(root, 1)) as Field as Uint<222> > radicand) "Math: sqrt underestimate (next root still ≤ radicand)";
        return root;
    }

    /**
     * @description Checks if a number is a multiple of another.
     *
     * @param value The number to check.
     * @param divisor The divisor to test against.
     * @returns Boolean True if value is a multiple of divisor, false otherwise.
     */
    export circuit isMultiple(
        value: Uint<128>,
        divisor: Uint<128>
    ): Boolean {
        assert divisor > 0 "Math: division by zero";
        return rem(value, divisor) == 0;
    }

    /**
     * @description Returns the minimum of two Uint<128> values.
     *
     * @param a The first number.
     * @param b The second number.
     * @returns Uint<128> The smaller of a and b.
     */
    export circuit min(
        a: Uint<128>,
        b: Uint<128>
    ): Uint<128> {
        return a < b ? a : b;
    }

    /**
     * @description Returns the maximum of two Uint<128> values.
     *
     * @param a The first number.
     * @param b The second number.
     * @returns Uint<128> The larger of a and b.
     */
    export circuit max(
        a: Uint<128>,
        b: Uint<128>
    ): Uint<128> {
        return a > b ? a : b;
    }
}
