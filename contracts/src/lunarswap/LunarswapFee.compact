// SPDX-License-Identifier: MIT
// OpenZeppelin Midnight Apps Contracts v0.0.1-alpha.0 (lunarswap-v1/LunarswapFee.compact)

pragma language_version >= 0.21.0;

/**
 * @title LunarswapFee
 * @description Fee management system for the Lunarswap protocol, handling protocol fee collection and distribution.
 *
 * @remarks
 * The LunarswapFee module manages the protocol's fee system, allowing for sustainable protocol development
 * and maintenance. It implements a flexible fee collection mechanism that can be enabled or disabled by
 * authorized administrators, with fees distributed to designated recipients.
 *
 * Key Features:
 * - Protocol fee enablement/disablement
 * - Fee recipient management
 * - Fee setter privilege transfer
 * - Access control for administrative functions
 * - Flexible fee distribution system
 */
module LunarswapFee {
  import { ZswapCoinPublicKey } from CompactStandardLibrary;

  import { callerZPK } from "../openzeppelin/Utils" prefix Utils_;

  /**
   * @ledger {ZswapCoinPublicKey} feeTo
   * @description The current protocol fee recipient address (unshielded).
   */
  export ledger feeTo: ZswapCoinPublicKey;

  /**
   * @ledger {ZswapCoinPublicKey} feeToSetter
   * @description The address with privilege to set the protocol fee recipient (unshielded).
   */
  export ledger feeToSetter: ZswapCoinPublicKey;

  /**
   * @title initialize circuit
   * @description Initializes the fee management system with the fee setter address.
   *
   * @remarks
   * This circuit sets up the initial fee configuration by setting the fee setter
   * address. The fee setter has the authority to change fee recipients and transfer
   * fee setter privileges.
   *
   * @circuitInfo k=11, rows=500
   *
   * @param {ZswapCoinPublicKey} feeToSetter_ - The address that can set protocol fees.
   *
   * @returns [] - No return values.
   */
  export circuit initialize(feeToSetter_: ZswapCoinPublicKey): [] {
    feeToSetter = disclose(feeToSetter_);
  }

  /**
   * @title getFeeTo circuit
   * @description Returns the current protocol fee recipient address.
   *
   * @remarks
   * This circuit provides read-only access to the current fee recipient address.
   * Returns zero address if no fee recipient has been set.
   *
   * @circuitInfo k=5, rows=48
   *
   * @returns {ZswapCoinPublicKey} - The current fee recipient address.
   */
  export circuit getFeeTo(): ZswapCoinPublicKey {
    return feeTo;
  }

  /**
   * @title getFeeToSetter circuit
   * @description Returns the current fee setter address.
   *
   * @remarks
   * This circuit provides read-only access to the address that has permission
   * to set the fee recipient and transfer fee setter privileges.
   *
   * @circuitInfo k=5, rows=48
   *
   * @returns {ZswapCoinPublicKey} - The current fee setter address.
   */
  export circuit getFeeToSetter(): ZswapCoinPublicKey {
    return feeToSetter;
  }

  /**
   * @title setFeeTo circuit
   * @description Sets the fee recipient address.
   *
   * @remarks
   * This circuit allows the fee setter to change the address that receives
   * protocol fees from trading pairs.
   *
   * Requirements:
   * - The caller must be the current fee setter
   *
   * @circuitInfo k=10, rows=571
   *
   * @param {ZswapCoinPublicKey} feeTo_ - The new fee recipient address.
   *
   * @throws {Error} "LunarswapFactory: forbidden" if the caller is not the fee setter.
   *
   * @returns [] - No return values.
   */
  // TODO: what is the difference between Admin and feeToSetter.
  export circuit setFeeTo(feeTo_: ZswapCoinPublicKey): [] {
    // TODO: add access control.
    assert(Utils_callerZPK() == feeToSetter, "LunarswapFee: setFeeTo() - forbidden");
    feeTo = disclose(feeTo_);
    return [];
  }

  /**
   * @title setFeeToSetter circuit
   * @description Transfers fee setter privileges to a new address.
   *
   * @remarks
   * This circuit allows the current fee setter to transfer their privileges to
   * another address. This is a one-way operation that cannot be undone.
   *
   * Requirements:
   * - The caller must be the current fee setter
   *
   * @circuitInfo k=10, rows=571
   *
   * @param {ZswapCoinPublicKey} feeToSetter_ - The new fee setter address.
   *
   * @throws {Error} "LunarswapFactory: forbidden" if the caller is not the fee setter.
   *
   * @returns [] - No return values.
   */
  export circuit setFeeToSetter(feeToSetter_: ZswapCoinPublicKey): [] {
    assert(Utils_callerZPK() == feeToSetter, "LunarswapFee: setFeeToSetter() - forbidden");
    feeToSetter = disclose(feeToSetter_);
    return [];
  }
}
